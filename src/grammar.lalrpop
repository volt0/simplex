use crate::ast::*;

grammar;

// Module

pub Module: Module = {
    <definitions:Definition*> => Module { definitions }
};

Definition: Definition = {
    <def:FunctionDefinition> => def,
};

// Functions

FunctionDefinition: Definition = {
    "proc" <name:Identifier> <signature:FunctionSignature> <body:FunctionBody> => Definition {
        name,
        value: DefinitionValue::Function(Function { signature, body }),
    }
};

FunctionSignature: FunctionSignature = {
    "(" <args:Comma<FunctionArgument>> ")" <return_type:TypeAnnotation?> => FunctionSignature {
        args, return_type
    }
}

FunctionArgument: FunctionArgument = {
    <name:Identifier> <arg_type:TypeAnnotation> => FunctionArgument {name, arg_type}
}

FunctionBody: FunctionBody = {
    ";" => FunctionBody::Forward,
    <body:BasicBlock> => FunctionBody::BasicBlock(body),
}

// Statements

Statement: Statement = {
    <items:BasicBlock> => Statement::BasicBlock(items),
    "let" <var:VariableSignature> "=" <value:Expression> ";" => Statement::Let(
         Variable { name: var.0, value_type: var.1, init_expression: Some(value) }
    ),
    "var" <var:VariableSignature> "=" <value:Expression> ";" => Statement::Var(
         Variable { name: var.0, value_type: var.1, init_expression: Some(value) }
    ),
    "if" <cond:Expression> <body:BasicBlock> => Statement::If(cond, body),
    "while" <cond:Expression> <body:BasicBlock> => Statement::While(cond, body),
    "for" "_" "=" <iterator:Expression> <body:BasicBlock> => Statement::For(None, iterator, body),
    "for" <var:VariableSignature> "=" <iterator:Expression> <body:BasicBlock> => Statement::For(
        Some(Variable { name: var.0, value_type: var.1, init_expression: None }),
        iterator,
        body
    ),
    "break" ";" => Statement::Break,
    "continue" ";" => Statement::Continue,
    "return" ";" => Statement::Return(None),
    "return" <expression:Expression> ";" => Statement::Return(Some(expression)),
    <expression:Expression> ";" => Statement::Expression(expression),
}

BasicBlock: BasicBlock = {
    "{" <statements:(<Statement>)*> "}" => BasicBlock { statements }
}

VariableSignature: (String, Option<Type>) = {
    <name:Identifier> <type_spec:TypeAnnotation?> => (name, type_spec),
}

// Expressions

PrimaryExpression: Box<Expression> = {
    <name:Identifier> => Box::new(Expression::Identifier(name)),
    <constant:Constant> => Box::new(Expression::Constant(constant)),
    "(" <expression:Expression> ")" => expression,
}

PostfixExpression: Box<Expression> = {
    PrimaryExpression,
    <val:PostfixExpression> "(" <args:Comma<Expression>> ")" => Box::new(Expression::Call(
        CallExpression(val, args)
    )),
    <val:PostfixExpression> "[" <expression:Expression> "]" => Box::new(Expression::ItemAccess(
        ItemAccessExpression(val, expression))
    ),
    <val:PostfixExpression> "." <name:Identifier> => Box::new(Expression::MemberAccess(
        MemberAccessExpression(val, name))
    ),
}

UnaryExpression: Box<Expression> = {
    PostfixExpression,
    "+" <arg:UnaryExpression> => Box::new(Expression::UnaryOperation(
        UnaryOperationExpression { operation: UnaryOperation::Plus, arg })
    ),
    "-" <arg:UnaryExpression> => Box::new(Expression::UnaryOperation(
        UnaryOperationExpression { operation: UnaryOperation::Minus, arg })
    ),
    "~" <arg:UnaryExpression> => Box::new(Expression::UnaryOperation(
        UnaryOperationExpression { operation: UnaryOperation::BitNot, arg })
    ),
    "!" <arg:UnaryExpression> => Box::new(Expression::UnaryOperation(
        UnaryOperationExpression { operation: UnaryOperation::LogicalNot, arg })
    ),
}

CastExpression: Box<Expression> = {
    UnaryExpression,
    <arg:CastExpression> "as" <type_spec:Type> => Box::new(Expression::Cast(
        CastExpression(arg, type_spec)
    )),
}

MultiplicativeExpression: Box<Expression> = {
    CastExpression,
    <a:MultiplicativeExpression> "*" <b:CastExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Mul, lhs:a, rhs:b }
    )),
    <a:MultiplicativeExpression> "/" <b:CastExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Div, lhs:a, rhs:b }
    )),
    <a:MultiplicativeExpression> "%" <b:CastExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Mod, lhs:a, rhs:b }
    )),
}

AdditiveExpression: Box<Expression> = {
    MultiplicativeExpression,
    <a:AdditiveExpression> "+" <b:MultiplicativeExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Add, lhs:a, rhs:b }
    )),
    <a:AdditiveExpression> "-" <b:MultiplicativeExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Sub, lhs:a, rhs:b }
    )),
}

ShiftExpression: Box<Expression> = {
    AdditiveExpression,
    <a:ShiftExpression> "<<" <b:AdditiveExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::ShiftLeft, lhs:a, rhs:b }
    )),
    <a:ShiftExpression> ">>" <b:AdditiveExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::ShiftRight, lhs:a, rhs:b }
    )),
}

BitwiseAndExpression: Box<Expression> = {
    ShiftExpression,
    <a:BitwiseAndExpression> "&" <b:ShiftExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::BitAnd, lhs:a, rhs:b }
    )),
}

BitwiseXorExpression: Box<Expression> = {
    BitwiseAndExpression,
    <a:BitwiseXorExpression> "^" <b:BitwiseAndExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::BitXor, lhs:a, rhs:b }
    )),
}

BitwiseOrExpression: Box<Expression> = {
    BitwiseXorExpression,
    <a:BitwiseOrExpression> "|" <b:BitwiseXorExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::BitOr, lhs:a, rhs:b }
    )),
}

ComparisonExpression: Box<Expression> = {
    BitwiseOrExpression,
    <a:ComparisonExpression> "<" <b:BitwiseOrExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Lt, lhs:a, rhs:b }
    )),
    <a:ComparisonExpression> ">" <b:BitwiseOrExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Gt, lhs:a, rhs:b }
    )),
    <a:ComparisonExpression> "<=" <b:BitwiseOrExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Le, lhs:a, rhs:b }
    )),
    <a:ComparisonExpression> ">=" <b:BitwiseOrExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Ge, lhs:a, rhs:b }
    )),
    <a:ComparisonExpression> "==" <b:BitwiseOrExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Eq, lhs:a, rhs:b }
    )),
    <a:ComparisonExpression> "!=" <b:BitwiseOrExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::Ne, lhs:a, rhs:b }
    )),
}

LogicalAndExpression: Box<Expression> = {
    ComparisonExpression,
    <a:LogicalAndExpression> "&&" <b:ComparisonExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::LogicalAnd, lhs:a, rhs:b }
    )),
}

LogicalOrExpression: Box<Expression> = {
    LogicalAndExpression,
    <a:LogicalOrExpression> "||" <b:LogicalAndExpression> => Box::new(Expression::BinaryOperation(
        BinaryOperationExpr { operation: BinaryOperation::LogicalOr, lhs:a, rhs:b }
    )),
}

Expression: Box<Expression> = {
    LogicalOrExpression,
    <condition:LogicalOrExpression> "?" <case_a:Expression> ":" <case_b:Expression> => Box::new(Expression::Conditional(
        ConditionalExpression(condition, case_a, case_b)
    )),
}

// Types

TypeAnnotation: Type = {
    ":" <type_spec:Type> => type_spec
}

Type: Type = {
    "i8" => Type::Integer(IntegerType::I8),
    "i16" => Type::Integer(IntegerType::I16),
    "i32" => Type::Integer(IntegerType::I32),
    "i64" => Type::Integer(IntegerType::I64),
    "u8" => Type::Integer(IntegerType::U8),
    "u16" => Type::Integer(IntegerType::U16),
    "u32" => Type::Integer(IntegerType::U32),
    "u64" => Type::Integer(IntegerType::U64),
    "f32" => Type::Float(FloatType::F32),
    "f64" => Type::Float(FloatType::F64),
    "bool" => Type::Boolean,
    "void" => Type::Void,
    <name:Identifier> => Type::Identifier(name),
}

// Syntax base

Identifier: String = <s:r"[_A-Za-z][_0-9A-Za-z]*"> => s.to_string();

Constant: Constant = {
    <value:IntegerConstant> => Constant::Integer(value),
    <value:RealConstant> => Constant::Float(value),
    <value:StringConstant> => Constant::String(value),
    "true" => Constant::True,
    "false" => Constant::False,
    "void" => Constant::Void,
}

IntegerConstant: i32 = <s:r"[0-9]+"> => s.parse::<i32>().unwrap();
RealConstant: f64 = <s:r#"[0-9]+"."[0-9]*([Ee][+-]?[0-9]+)?[fFlL]?"#> => s.parse::<f64>().unwrap();
StringConstant: String = <s:r#""(\\.|[^\\"])*""#> => s.to_string();

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};
